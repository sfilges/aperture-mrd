process SAMBAMBA_MARKDUP {
    tag "$meta.id"
    label 'process_medium'

    container 'quay.io/biocontainers/sambamba:1.0.1--h6f6fda4_0'

    input:
    tuple val(meta), path(bam)

    output:
    tuple val(meta), path("*.bam"), emit: bam
    tuple val(meta), path("*.bai"), emit: bai, optional: true
    tuple val("${task.process}"), val('sambamba'), eval("echo \$(sambamba --version 2>&1) | awk '{print \$2}'"), emit: versions_sambamba, topic: versions

    when:
    task.ext.when == null || task.ext.when

    script:
    def args = task.ext.args ?: ''
    def prefix = task.ext.prefix ?: "${meta.id}"

    """
    sambamba \\
        markdup \\
        $args \\
        -t $task.cpus \\
        --tmpdir ./ \\
        $bam \\
        ${prefix}.bam
    """
}